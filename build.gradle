defaultTasks 'clean', 'build'

buildscript {
    ext.kotlin_version = '1.1.2-3'
    ext.vaadin_version = '8.1.0.beta1'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    group 'com.example.vok'
    version '1.0-SNAPSHOT'
}

subprojects {

    apply plugin: 'kotlin'

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            jvmTarget = "1.8"
            apiVersion = "1.1"
            languageVersion = "1.1"
        }
    }

    repositories {
        mavenCentral()
        jcenter()
        maven {
            name 'vaadin-addons'
            url 'http://maven.vaadin.com/vaadin-addons'
        }
        maven {
            name 'vaadin-prereleases'
            url 'https://maven.vaadin.com/vaadin-prereleases'
        }
        maven {
            url "https://dl.bintray.com/mvysny/vaadin-on-kotlin"
        }
    }

    // for Travis to record failures: http://stackoverflow.com/questions/28614865/how-to-read-test-result-reports-on-travis-ci/28617954#28617954
    test {
        testLogging {
            events "failed"
            exceptionFormat "full"
        }
    }
}
